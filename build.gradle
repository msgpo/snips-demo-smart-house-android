buildscript {
    ext.kotlin_version = '1.3.31'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

repositories {
    mavenLocal()
    maven {
        url "https://nexus-repository.snips.ai/repository/snips-maven-releases/"
    }
}

def versionMajor = 0
def versionMinor = 8
def versionPatch = 3

version = "${versionMajor}.${versionMinor}.${versionPatch}"
def versionNumber = 10000 * versionMajor + 100 * versionMinor + versionPatch

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "ai.snips.smarthousedemo"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode versionNumber
        versionName version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'lib/armeabi/*'
        exclude 'lib/mips/*'
        exclude 'lib/mips64/*'
    }

    // add version to output filename
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "${project.name}-${output.baseName}-${variant.versionName}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation('ai.snips:snips-platform-android:0.63.3@aar') {
        transitive = true
    }
}

